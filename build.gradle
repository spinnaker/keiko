buildscript {
  repositories {
    jcenter()
    maven { url "https://spinnaker.bintray.com/gradle" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath("com.netflix.spinnaker.gradle:spinnaker-dev-plugin:$spinnakerGradleVersion")
    if (Boolean.valueOf(enablePublishing)) {
      classpath("com.netflix.spinnaker.gradle:spinnaker-gradle-project:$spinnakerGradleVersion")
    }
  }
}

plugins {
  id("nebula.kotlin").version("1.3.31").apply(false)
  id("org.jetbrains.kotlin.plugin.spring").version("1.3.31").apply(false)
  id("org.jetbrains.dokka").version("0.9.18").apply(false)
}

allprojects {
  apply(plugin: "spinnaker.base-project")
  if (Boolean.valueOf(enablePublishing)) {
    apply(plugin: "spinnaker.project")
  }

  if (korkVersion.endsWith('-SNAPSHOT')) {
    repositories {
      mavenLocal()
    }
  }

  group = "com.netflix.spinnaker.keiko"
}

subprojects {
  apply(from: "$rootDir/gradle/kotlin.gradle")
  apply(from: "$rootDir/gradle/dokka.gradle")

  repositories {
    jcenter()
  }

  dependencies {
    implementation(platform("com.netflix.spinnaker.kork:kork-bom:$korkVersion"))
    annotationProcessor(platform("com.netflix.spinnaker.kork:kork-bom:${property("korkVersion")}"))
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
    implementation("org.slf4j:slf4j-api")
    testRuntimeOnly("org.slf4j:slf4j-nop")
  }
}

apply(from: "$rootDir/gradle/ktlint-root.gradle")

defaultTasks "build"
